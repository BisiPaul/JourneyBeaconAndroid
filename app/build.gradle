apply plugin: 'com.android.application'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs'

def localKeystoreProperties = new Properties()
try {
    localKeystoreProperties.load(new FileInputStream(rootProject.file("localkeystore.properties")))
} catch (Exception e) {
    logger.warn("No Local Keystore Properties File Found!")
}

android {
    signingConfigs {
        debug {
            if (System.getenv("DEBUG_KEY_PASSWORD") != null
                    && System.getenv("DEBUG_STORE_PASSWORD") != null) {
                keyAlias 'acky-debug'
                keyPassword System.getenv("DEBUG_KEY_PASSWORD")
                storeFile file('../app/acky-debug.jks')
                storePassword System.getenv("DEBUG_STORE_PASSWORD")
            } else {
                keyAlias localKeystoreProperties['keyAlias']
                keyPassword localKeystoreProperties['keyPassword']
                storeFile file(localKeystoreProperties['storeFile'])
                storePassword localKeystoreProperties['storePassword']
            }
        }
        release {
            if (System.getenv("RELEASE_KEY_PASSWORD") != null
                    && System.getenv("RELEASE_STORE_PASSWORD") != null) {
                keyAlias 'journeybeacon-prod'
                keyPassword System.getenv("RELEASE_KEY_PASSWORD")
                storeFile file('/Users/paulbisioc/Desktop/kstore/JourneyBeacon/journeybeacon-prod.jks')
                storePassword System.getenv("RELEASE_STORE_PASSWORD")
            } else {
                keyAlias localKeystoreProperties['keyAlias']
                keyPassword localKeystoreProperties['keyPassword']
                storeFile file(localKeystoreProperties['storeFile'])
                storePassword localKeystoreProperties['storePassword']
            }
        }
    }

    compileSdkVersion 33
    buildToolsVersion "33.0.0"

    defaultConfig {
        applicationId "com.fluffydevs.journeybeacon"
        minSdkVersion 24
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    namespace 'com.fluffydevs.journeybeacon'
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // Android JetPack
    implementation "androidx.activity:activity-ktx:1.7.1"
    implementation "androidx.fragment:fragment-ktx:1.5.7"

    // Live Data
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.1'

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.10'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"

    // Dependency Injection
    // https://developer.android.com/training/dependency-injection/hilt-android
    implementation 'com.google.dagger:hilt-android:2.46'
    kapt 'com.google.dagger:hilt-android-compiler:2.46'
    kapt 'androidx.hilt:hilt-compiler:1.0.0'

    // Timber
    implementation "com.jakewharton.timber:timber:5.0.1"

    // Google Play Auth
    implementation 'com.google.android.gms:play-services-auth:20.5.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.7.2'

    // GSON
    implementation 'com.google.code.gson:gson:2.9.0'

    // Logging Interceptor
    implementation "com.squareup.okhttp3:okhttp:3.14.9"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.4.1'

    // AltBeacon
    implementation 'org.altbeacon:android-beacon-library:2.19.5'
}

repositories {
    mavenCentral()
}